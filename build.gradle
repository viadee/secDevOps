buildscript {
	dependencies {
		// Force guava 23.1 because of conflicts between spotbugs and docker plugins
        classpath 'com.google.guava:guava:23.1-jre'
    }
}

plugins {
	id 'org.springframework.boot' version '2.1.8.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'
	id 'com.github.spotbugs' version '2.0.0'
	id 'com.palantir.docker' version '0.22.1'
	id 'war'
	id 'idea'
	id 'eclipse'
}

group = 'de.cqrity'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

bootRun {
	main = 'de.cqrity.vulnerapp.Vulnerapp'
}

bootWar {
	excludeDevtools = true
}

spotbugs {
	ignoreFailures = true
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled true
        xml.withMessages true
    }
}

docker {
	dependsOn bootWar
	name "${project.group}/${bootWar.baseName}"
	files bootWar.archivePath, 'assets'
	buildArgs(['WAR_FILE': "${bootWar.archiveName}"])
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.security:spring-security-taglibs'
	implementation 'org.apache.tomcat.embed:tomcat-embed-jasper'
	implementation 'commons-codec:commons-codec:1.10'
	implementation 'com.google.zxing:javase:3.3.0'
	implementation 'org.owasp.esapi:esapi:2.1.0.1'
	implementation 'org.owasp.encoder:encoder:1.2.1'
	implementation 'org.owasp.encoder:encoder-jsp:1.2.1'
	implementation 'javax.servlet:jstl:1.2'
	implementation 'com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20170515.1'
	implementation 'com.google.guava:guava:23.0'
	implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.1'
	
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	
	implementation 'org.hsqldb:hsqldb'
	
	spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.7.1'
}

